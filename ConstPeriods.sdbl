ВЫБРАТЬ
    ПодразделенияФизЛиц.ФизЛицо КАК ФизЛицо,
    ПодразделенияФизЛиц.Период КАК Период,
    ПодразделенияФизЛиц.Занятость КАК Занятость
ПОМЕСТИТЬ НачалаПериодов
ИЗ
    РегистрСведений.ПодразделенияФизЛиц КАК ПодразделенияФизЛиц
        ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодразделенияФизЛиц КАК ПодразделенияФизЛиц1
        ПО ПодразделенияФизЛиц.ФизЛицо = ПодразделенияФизЛиц1.ФизЛицо
            И (РАЗНОСТЬДАТ(ПодразделенияФизЛиц.Период, ПодразделенияФизЛиц1.Период, ДЕНЬ) = -1)
ГДЕ
    ПодразделенияФизЛиц.Занятость <> ЕСТЬNULL(ПодразделенияФизЛиц1.Занятость, НЕОПРЕДЕЛЕНО)

ИНДЕКСИРОВАТЬ ПО
    ФизЛицо,
    Занятость,
    Период
;

////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
    ПодразделенияФизЛиц.ФизЛицо КАК ФизЛицо,
    ПодразделенияФизЛиц.Период КАК Период,
    ПодразделенияФизЛиц.Занятость КАК Занятость
ПОМЕСТИТЬ ОкончанияПериодов
ИЗ
    РегистрСведений.ПодразделенияФизЛиц КАК ПодразделенияФизЛиц
        ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодразделенияФизЛиц КАК ПодразделенияФизЛиц1
        ПО ПодразделенияФизЛиц.ФизЛицо = ПодразделенияФизЛиц1.ФизЛицо
            И (РАЗНОСТЬДАТ(ПодразделенияФизЛиц.Период, ПодразделенияФизЛиц1.Период, ДЕНЬ) = 1)
ГДЕ
    ПодразделенияФизЛиц.Занятость <> ЕСТЬNULL(ПодразделенияФизЛиц1.Занятость, НЕОПРЕДЕЛЕНО)

ИНДЕКСИРОВАТЬ ПО
    ФизЛицо,
    Занятость,
    Период
;

////////////////////////////////////////////////////////////////////////////////

ВЫБРАТЬ
    НачалаПериодов.ФизЛицо КАК ФизЛицо,
    НачалаПериодов.Период КАК Начало,
    МИНИМУМ(ОкончанияПериодов.Период) КАК Окончание,
    НачалаПериодов.Занятость
ИЗ
    НачалаПериодов КАК НачалаПериодов
        ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОкончанияПериодов КАК ОкончанияПериодов
        ПО НачалаПериодов.ФизЛицо = ОкончанияПериодов.ФизЛицо
            И НачалаПериодов.Занятость = ОкончанияПериодов.Занятость
            И НачалаПериодов.Период <= ОкончанияПериодов.Период

СГРУППИРОВАТЬ ПО
    НачалаПериодов.ФизЛицо,
    НачалаПериодов.Период,
    НачалаПериодов.Занятость

УПОРЯДОЧИТЬ ПО
    ФизЛицо,
    Начало
АВТОУПОРЯДОЧИВАНИЕ
